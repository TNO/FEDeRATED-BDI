
plugins {
    id 'net.corda.plugins.cordapp' version "$corda_gradle_plugin_version"
    id 'net.corda.plugins.quasar-utils' version "$corda_gradle_plugin_version"
}

apply plugin: 'kotlin'

cordapp {
    targetPlatformVersion corda_platform_version
    minimumPlatformVersion corda_platform_version
    workflow {
        name "Template Flows"
        vendor "Corda Open Source"
        licence "Apache License, Version 2.0"
        versionId 4
    }
}

dependencies {
    cordapp project(":corda:contracts")

    // Corda dependencies.
    cordaCompile "net.corda:corda-core"
    cordaRuntime "net.corda:corda"

    compile project(":ishare")

    // We need to use compile instead of implementation because the corda plugin does not recognize implementation
    // and the dependency wont be included in the generated cordapp workflows.jar
    compile "com.fasterxml.jackson.module:jackson-module-kotlin"
    compile "org.eclipse.rdf4j:rdf4j-rio-turtle"
    compile "org.eclipse.rdf4j:rdf4j-rio-api"

    // JSON Web Token support (for iSHARE)
    compile "io.jsonwebtoken:jjwt-impl:0.11.5"
    compile "io.jsonwebtoken:jjwt-jackson:0.11.5"
    // JWKS verification (for iSHARE)
    compile "org.bitbucket.b_c:jose4j:0.7.12"

    testImplementation "junit:junit"
    testImplementation "io.mockk:mockk"
    testImplementation "org.testcontainers:testcontainers"
    testImplementation "org.jetbrains.kotlin:kotlin-test"
    testImplementation "net.corda:corda-node-driver"
}

test {
    maxHeapSize = "4096m"
}

jar {
    // This makes the JAR's SHA-256 hash repeatable.
    preserveFileTimestamps = false
    reproducibleFileOrder = true
    // Because the cordapp flattens the jar (fat jar) we need to make sure no duplicate entries end up in the jar.
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    exclude('database.properties')
    exclude('ishare.properties')
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
    kotlinOptions {
        languageVersion = "1.2" // corda 4.9 supports kotlin 1.2.71
        apiVersion = "1.2" // corda 4.9 supports kotlin 1.2.71
        jvmTarget = "1.8"
        javaParameters = true   // Useful for reflection.
    }
}