buildscript {
    ext {
        //https://docs.r3.com/en/platform/corda/4.9/community/features-versions.html
        //https://docs.r3.com/en/platform/corda/4.9/community/app-upgrade-notes.html
        corda_release_version = "4.9.2" // this property must be specified for the cordformation plugin
        corda_platform_version = 4
        corda_gradle_plugin_version = "5.0.15"
        spring_boot_version = "2.3.12.RELEASE"
        kotlin_version = "1.3.72"
        log4j_version = "2.19.0"
        slf4j_version = "1.7.25"
        junit_version = "4.13.1"
        jackson_version = "2.9.7"
        springfox_version = "2.7.0" // 3.0.0 does not work with spring-boot 2.3.12.RELEASE
        activemq_version = "2.12.0"
        rdf4j_version = "3.7.4"
    }
}

plugins {
    // the kotlin gradle plugin must be included in the root project, but dont apply it
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_version" apply false
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'https://software.r3.com/artifactory/corda' }
        maven { url 'https://software.r3.com/artifactory/corda-releases' }
        maven { url 'https://jitpack.io' }
        maven { url 'https://repo.gradle.org/gradle/libs-releases' }
    }
}

subprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }

    configurations.all {
        resolutionStrategy {
            // fail eagerly on version conflict (includes transitive dependencies)
            // e.g. multiple different versions of the same dependency (group and name are equal)
            failOnVersionConflict()

            // prefer modules that are part of this build (multi-project or composite build) over external modules
            preferProjectModules()

            // force certain versions of dependencies (including transitive)
            force "net.corda:corda:$corda_release_version"
            force "net.corda:corda-core:$corda_release_version"
            force "net.corda:corda-node-api:$corda_release_version"
            force "net.corda:corda-node-driver:$corda_release_version"
            force "net.corda:corda-rpc:$corda_release_version"
            force "net.corda:corda-shell:$corda_release_version"

            force "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
            force "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
            force "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
            force "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
            force "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
            force "org.jetbrains.kotlin:kotlin-test:$kotlin_version"

            force "org.apache.logging.log4j:log4j-core:$log4j_version"
            force "com.fasterxml.jackson.module:jackson-module-kotlin:$jackson_version"
            force "org.apache.activemq:artemis-core-client:$activemq_version"
            force "org.apache.activemq:artemis-amqp-protocol:$activemq_version"

            force "be.ugent.rml:rmlmapper:6.0.0"
            force "org.apache.jena:apache-jena-libs:3.8.0"
            force "io.springfox:springfox-swagger2:$springfox_version"
            force "io.springfox:springfox-swagger-ui:$springfox_version"
            force "org.eclipse.rdf4j:rdf4j-rio-turtle:$rdf4j_version"
            force "org.eclipse.rdf4j:rdf4j-rio-api:$rdf4j_version"

            force "junit:junit:$junit_version"
            force "io.mockk:mockk:1.9.3"
            force "org.testcontainers:testcontainers:1.17.5"
            force "org.mockito.kotlin:mockito-kotlin:4.0.0"
        }
    }
}
